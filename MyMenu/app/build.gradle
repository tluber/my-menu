apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def appName = "My Menu"

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    defaultConfig {
        applicationId "com.acp1.my.menu"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }
    buildTypes {
        debug {
            minifyEnabled false
            debuggable true
            versionNameSuffix '-beta'
        }
        release {
            minifyEnabled true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/atomicfu.kotlin_module'
    }

    lintOptions {
        abortOnError false
    }

    androidExtensions {
        experimental = true
    }

    flavorDimensions "default"
    productFlavors {
        dev {
            versionNameSuffix "-dev"
            manifestPlaceholders = [appName: "${appName}$versionNameSuffix"]
            buildConfigField 'String', 'HOST', '"https://acp1-2019-2c.herokuapp.com"'
            buildConfigField 'String', 'API_KEY', '""'
        }
        prod {
            manifestPlaceholders = [appName: "${appName}"]
            buildConfigField 'String', 'HOST', '"https://acp1-2019-2c.herokuapp.com"'
            buildConfigField 'String', 'API_KEY', '""'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.3.50"

    // Base: https://developer.android.com/jetpack/androidx/migrate
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.2.0-beta01'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta2'
    implementation 'com.google.android.material:material:1.1.0-beta01'
    implementation "androidx.annotation:annotation:1.1.0"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation 'androidx.recyclerview:recyclerview:1.1.0-beta05'
    implementation "androidx.viewpager:viewpager:1.0.0"
    implementation 'org.apache.commons:commons-lang3:3.8.1'

    //Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.0"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.0'

    // LiveData + ViewModel : https://developer.android.com/topic/libraries/architecture/adding-components
    def lifecycle_version = "2.2.0-alpha04"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"

    // Image Management Library: https://frescolib.org/
    implementation 'com.facebook.fresco:fresco:2.0.0'

    // Retrofit: http://square.github.io/retrofit/
    def retrofit_version = "2.5.0"
    def okHttp_version = "3.12.0"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.okhttp3:okhttp:$okHttp_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okHttp_version"
    implementation "com.jakewharton.retrofit:retrofit2-kotlin-coroutines-adapter:0.9.2"

    // Moshi: https://github.com/square/moshi
    def moshiVersion = "1.8.0"
    implementation "com.squareup.moshi:moshi:$moshiVersion"
    implementation "com.squareup.moshi:moshi-adapters:$moshiVersion"
    implementation "com.squareup.moshi:moshi-kotlin:$moshiVersion"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshiVersion"

    def daggerVer = "2.24"
    implementation "com.google.dagger:dagger:$daggerVer"
    implementation "com.google.dagger:dagger-android-support:$daggerVer"
    implementation "com.google.dagger:dagger-android:$daggerVer"
    kapt "com.google.dagger:dagger-compiler:$daggerVer"
    kapt "com.google.dagger:dagger-android-processor:$daggerVer"

    // Debug WebServices: http://facebook.github.io/stetho/
    implementation 'com.facebook.stetho:stetho:1.5.1'
    implementation 'com.facebook.stetho:stetho-okhttp3:1.5.1'

    // Debug: database: https://github.com/amitshekhariitbhu/Android-Debug-Database
    implementation 'com.amitshekhar.android:debug-db:1.0.6'

    // Testing
    testImplementation 'junit:junit:4.13-beta-3'
    androidTestImplementation 'androidx.test:runner:1.3.0-alpha02'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0-alpha02'
}
